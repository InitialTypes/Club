{-# OPTIONS --postfix-projections #-} -- To work interactively with copatterns

-- Cartesian closed categories

module CCC where

open import Agda.Primitive  -- Universe levels
open import Relation.Binary using (Setoid; IsEquivalence); open Setoid; open IsEquivalence
import Relation.Binary.EqReasoning as EqR


---------------------------------------------------------------------------
-- Cartesian closed category as mathematical structure
---------------------------------------------------------------------------

record CCC o m e : Set (lsuc (o âŠ” m âŠ” e)) where

  ---------------------------------------------------------------------------
  -- Category
  ---------------------------------------------------------------------------

  field
    -- Objects.  We use propositional equality for objects.
    Ob  : Set o

    -- Morphisms.  The equality may be non-trivial.
    Homs : (a b : Ob) â†’ Setoid m e

  -- Abbreviations to access Hom-set and its equality

  Hom : (a b : Ob) â†’ Set m
  Hom a b = Homs a b .Carrier

  Eq : âˆ€{a b} (f g : Hom a b) â†’ Set e
  Eq f g = Homs _ _ ._â‰ˆ_ f g

  eq-refl : âˆ€{a b} {f : Hom a b} â†’ Eq f f
  eq-refl {a} {b} {f} = Homs a b .isEquivalence .refl {f}

  eq-sym : âˆ€{a b} {f g : Hom a b} â†’ Eq g f â†’ Eq f g
  eq-sym e = Homs _ _ .isEquivalence .sym e

  eq-trans : âˆ€{a b} {f g h : Hom a b} â†’ Eq f g â†’ Eq g h â†’ Eq f h
  eq-trans e e' = Homs _ _ .isEquivalence .trans e e'

  field
    -- Category operations
    id   : (a : Ob) â†’ Hom a a
    comp : {a b c : Ob} (f : Hom b c) (g : Hom a b) â†’ Hom a c

    -- Category laws
    id-l : âˆ€{a b} (f : Hom a b)
      â†’ Eq (comp (id b) f) f
    id-r : âˆ€{a b} (f : Hom a b)
      â†’ Eq (comp f (id a)) f
    assoc : âˆ€{a b c d} (f : Hom c d) (g : Hom b c) (h : Hom a b)
      â†’ Eq (comp (comp f g) h) (comp f (comp g h))

    comp-cong : âˆ€{a b c} {f f' : Hom b c} {g g' : Hom a b}
      â†’ Eq f f'
      â†’ Eq g g'
      â†’ Eq (comp f g) (comp f' g')

  ---------------------------------------------------------------------------
  -- Cartesian
  ---------------------------------------------------------------------------

  field
    -- Product object and projections
    Prod : (a b : Ob) â†’ Ob
    Ï€â‚    : âˆ€{a b} â†’ Hom (Prod a b) a
    Ï€â‚‚    : âˆ€{a b} â†’ Hom (Prod a b) b

  -- Properties of candidates for the pairing function

  IsPairâ‚ : âˆ€{a b c} (f : Hom c a) (h : Hom c (Prod a b)) â†’ Set _
  IsPairâ‚ f h = Eq (comp Ï€â‚ h) f

  IsPairâ‚‚ : âˆ€{a b c} (g : Hom c b) (h : Hom c (Prod a b)) â†’ Set _
  IsPairâ‚‚ g h = Eq (comp Ï€â‚‚ h) g

  field
    -- Pairing and Î²-laws
    pair : âˆ€{a b c} (f : Hom c a) (g : Hom c b) â†’ Hom c (Prod a b)
    Î²-Ï€â‚  : âˆ€{a b c} {f : Hom c a} {g : Hom c b} â†’ IsPairâ‚ f (pair f g)
    Î²-Ï€â‚‚  : âˆ€{a b c} {f : Hom c a} {g : Hom c b} â†’ IsPairâ‚‚ g (pair f g)

    -- Uniqueness of pairing
    pair-unique : âˆ€{a b c} (f : Hom c a) (g : Hom c b) (h : Hom c (Prod a b))
      â†’ IsPairâ‚ f h
      â†’ IsPairâ‚‚ g h
      â†’ Eq h (pair f g)

  ---------------------------------------------------------------------------
  field
    -- Terminal object
    Unit : Ob
    unit : âˆ€ a â†’ Hom a Unit

    -- Uniqueness of terminal morphism
    unit-unique : âˆ€{a} (h : Hom a Unit)
      â†’ Eq h (unit a)

  ---------------------------------------------------------------------------
  -- Closed
  ---------------------------------------------------------------------------

  -- Lift a morphism under a binder.
  -- lift f = f Ã— id

  lift : âˆ€{a b} (f : Hom a b) {c} â†’ Hom (Prod a c) (Prod b c)
  lift f = pair (comp f Ï€â‚) Ï€â‚‚

  field
    -- Exponential object and application
    Arr : (a b : Ob) â†’ Ob
    apply : âˆ€{a b} â†’ Hom (Prod (Arr a b) a) b

  IsCurry : âˆ€{a b c} (f : Hom (Prod c a) b) (h : Hom c (Arr a b)) â†’ Set _
  IsCurry f h = Eq (comp apply (lift h)) f

  field
    curry   : âˆ€{a b c} (f : Hom (Prod c a) b) â†’ Hom c (Arr a b)
    Î²-apply : âˆ€{a b c} (f : Hom (Prod c a) b) â†’ IsCurry f (curry f)

    curry-unique : âˆ€{a b c} (f : Hom (Prod c a) b) (h : Hom c (Arr a b))
      â†’ IsCurry f h
      â†’ Eq h (curry f)

  ---------------------------------------------------------------------------
  -- Derived laws for products
  ---------------------------------------------------------------------------

  -- Congruence law for pairing

  pair-cong : âˆ€{a b c} {f f' : Hom c a} {g g' : Hom c b}
    â†’ Eq f f'
    â†’ Eq g g'
    â†’ Eq (pair f g) (pair f' g')
  pair-cong {a} {b} {c} {f} {f'} {g} {g'} e e' = pair-unique f' g' (pair f g) ef eg
    where
    ef : Eq (comp Ï€â‚ (pair f g)) f'
    ef = eq-trans Î²-Ï€â‚ e
    eg : Eq (comp Ï€â‚‚ (pair f g)) g'
    eg = eq-trans Î²-Ï€â‚‚ e'

  -- Naturality law for pairing

  pair-nat : âˆ€{a b c d} (f : Hom c a) (g : Hom c b) (h : Hom d c)
    â†’ Eq (comp (pair f g) h) (pair (comp f h) (comp g h))
  pair-nat {a} {b} {c} {d} f g h =
      pair-unique (comp f h) (comp g h) (comp (pair f g) h) ef eg
    where

    ef : Eq (comp Ï€â‚ (comp (pair f g) h)) (comp f h)
    ef = begin
      comp Ï€â‚ (comp (pair f g) h)   â‰ˆâŸ¨ eq-sym (assoc _ _ _) âŸ©
      comp (comp Ï€â‚ (pair f g)) h   â‰ˆâŸ¨ comp-cong Î²-Ï€â‚ eq-refl âŸ©
      comp f h                      âˆ where open EqR (Homs _ _)

    eg : Eq (comp Ï€â‚‚ (comp (pair f g) h)) (comp g h)
    eg = begin
      comp Ï€â‚‚ (comp (pair f g) h)   â‰ˆâŸ¨ eq-sym (assoc _ _ _) âŸ©
      comp (comp Ï€â‚‚ (pair f g)) h   â‰ˆâŸ¨ comp-cong Î²-Ï€â‚‚ eq-refl âŸ©
      comp g h                      âˆ where open EqR (Homs _ _)

  -- Lemma: id is a pairing of Ï€â‚ and Ï€â‚‚

  isPair-Ï€â‚-id : âˆ€{a b} â†’ IsPairâ‚ Ï€â‚ (id (Prod a b))
  isPair-Ï€â‚-id {a} {b} = id-r _

  isPair-Ï€â‚‚-id : âˆ€{a b} â†’ IsPairâ‚‚ Ï€â‚‚ (id (Prod a b))
  isPair-Ï€â‚‚-id {a} {b} = id-r _

  -- Thus, the pairing of Ï€â‚ and Ï€â‚‚ is the identity by uniqueness of pairing

  pair-Ï€ : âˆ€{a b} â†’ Eq (pair Ï€â‚ Ï€â‚‚) (id (Prod a b))
  pair-Ï€ = eq-sym (pair-unique Ï€â‚ Ï€â‚‚ (id _) isPair-Ï€â‚-id isPair-Ï€â‚‚-id)

  ---------------------------------------------------------------------------
  -- Derived laws for lifting
  ---------------------------------------------------------------------------

  comp-lift-lift : âˆ€{a b c d} (f : Hom b c) (g : Hom a b)
    â†’ Eq (comp (lift f) (lift g)) (lift (comp f g) {d})
  comp-lift-lift {a} {b} {c} {d} f g = begin
      comp (pair (comp f Ï€â‚) Ï€â‚‚) (pair (comp g Ï€â‚) Ï€â‚‚)  â‰ˆâŸ¨ pair-nat _ _ _ âŸ©
      pair (comp (comp f Ï€â‚) (pair (comp g Ï€â‚) Ï€â‚‚))
           (comp Ï€â‚‚ (pair (comp g Ï€â‚) Ï€â‚‚))              â‰ˆâŸ¨ pair-cong (assoc _ _ _) Î²-Ï€â‚‚ âŸ©
      pair (comp f (comp Ï€â‚ (pair (comp g Ï€â‚) Ï€â‚‚))) Ï€â‚‚  â‰ˆâŸ¨ pair-cong (comp-cong eq-refl Î²-Ï€â‚) eq-refl âŸ©
      pair (comp f (comp g Ï€â‚)) Ï€â‚‚                      â‰ˆâŸ¨ pair-cong (eq-sym (assoc _ _ _)) eq-refl âŸ©
      pair (comp (comp f g) Ï€â‚) Ï€â‚‚
      âˆ where open EqR (Homs _ _)

  lift-comp : âˆ€{a b c d} (f : Hom b c) (g : Hom a b)
    â†’ Eq (lift (comp f g) {d}) (comp (lift f) (lift g))
  lift-comp {a} {b} {c} {d} f g = eq-sym (comp-lift-lift f g)

  ---------------------------------------------------------------------------
  -- Derived laws for exponentials
  ---------------------------------------------------------------------------

  -- Congruence law for currying

  curry-cong : âˆ€{a b c} {f f' : Hom (Prod c a) b}
    â†’ (e : Eq f f')
    â†’ Eq (curry f) (curry f')
  curry-cong {a} {b} {c} {f} {f'} e = curry-unique f' (curry f) eq
    where
    eq : Eq (comp apply (lift (curry f))) f'
    eq = eq-trans (Î²-apply f) e

  -- Naturality law for currying

  curry-nat : âˆ€{a b c d} (f : Hom (Prod c a) b) (h : Hom d c)
    â†’ Eq (comp (curry f) h)
         (curry (comp f (lift h)))
  curry-nat {a} {b} {c} {d} f h =
      curry-unique (comp f (lift h)) (comp (curry f) h) eq
    where
    eq : Eq (comp apply (lift (comp (curry f) h)))
            (comp f (lift h))
    eq = begin
      comp apply (lift (comp (curry f) h))         â‰ˆâŸ¨ comp-cong eq-refl (lift-comp _ _) âŸ©
      comp apply (comp (lift (curry f)) (lift h))  â‰ˆâŸ¨ eq-sym (assoc _ _ _) âŸ©
      comp (comp apply (lift (curry f))) (lift h)  â‰ˆâŸ¨ comp-cong (Î²-apply _) eq-refl âŸ©
      comp f (lift h) âˆ where open EqR (Homs _ _)


  -- Lemma: id is a currying of the apply morphism

  isCurry-apply-id : âˆ€ {a b} â†’ IsCurry apply (id (Arr a b))
  isCurry-apply-id {a} {b} = begin
    comp apply (lift (id (Arr a b)))  â‰ˆâŸ¨ comp-cong eq-refl

     (beginâ€²
      lift (id (Arr a b))             â‰ˆâŸ¨ pair-cong (id-l _) eq-refl âŸ©â€²
      pair Ï€â‚ Ï€â‚‚                      â‰ˆâŸ¨ pair-Ï€ âŸ©â€²
      id _
      âˆâ€² )âŸ©

    comp apply (id _)                 â‰ˆâŸ¨ id-r _ âŸ©
    apply
    âˆ where
      open EqR (Homs _ _)
      open EqR (Homs _ _) using () renaming (begin_ to beginâ€²_; _âˆ to _âˆâ€²; _â‰ˆâŸ¨_âŸ©_ to _â‰ˆâŸ¨_âŸ©â€²_)

  -- Thus, curry apply is the identity by uniqueness of currying.

  curry-apply : âˆ€{a b} â†’ Eq (curry apply) (id (Arr a b))
  curry-apply = eq-sym (curry-unique apply (id _) isCurry-apply-id)


-- Interpret the CCCInternalLanguage in an arbitrary CCC

module Sound {o m e} (C : CCC o m e) where

  open module Cat = CCC C using (Ob; Unit; Prod; Arr)

  open import Types
  open import CCCInternalLanguage

  âŸ¦_âŸ§ : Ty â†’ Ob  -- \[[   \]]
  âŸ¦ ğŸ™ âŸ§     = Unit
  âŸ¦ a â‡’ b âŸ§ = Arr âŸ¦ a âŸ§ âŸ¦ b âŸ§
  âŸ¦ a * b âŸ§ = Prod âŸ¦ a âŸ§ âŸ¦ b âŸ§

  â¦…_â¦† : âˆ€{a b} â†’ Hom a b â†’ Cat.Hom âŸ¦ a âŸ§ âŸ¦ b âŸ§  -- \((  \))
  â¦… id â¦†       = Cat.id _
  â¦… f âˆ˜ g â¦†    = Cat.comp â¦… f â¦† â¦… g â¦†
  â¦… fst â¦†      = Cat.Ï€â‚
  â¦… snd â¦†      = Cat.Ï€â‚‚
  â¦… pair f g â¦† = Cat.pair â¦… f â¦† â¦… g â¦†
  â¦… unit â¦†     = Cat.unit _
  â¦… curry f â¦†  = Cat.curry â¦… f â¦†
  â¦… apply â¦†    = Cat.apply

  âŸª_âŸ« : âˆ€{a b} {f g : Hom a b} â†’ f ~ g â†’ Cat.Eq â¦… f â¦† â¦… g â¦†  -- \<<  \>>
  âŸª id-l âŸ«          = Cat.id-l _
  âŸª id-r âŸ«          = Cat.id-r _
  âŸª assoc âŸ«         = Cat.assoc _ _ _
  âŸª fst-pair âŸ«      = Cat.Î²-Ï€â‚
  âŸª snd-pair âŸ«      = Cat.Î²-Ï€â‚‚
  âŸª id-pair âŸ«       = Cat.eq-sym Cat.pair-Ï€
  âŸª pair-comp âŸ«     = Cat.pair-nat _ _ _
  âŸª unit âŸ«          = Cat.unit-unique _
  âŸª apply-curry âŸ«   = Cat.Î²-apply _
  âŸª curry-apply âŸ«   = Cat.curry-apply
  âŸª curry-comp âŸ«    = Cat.curry-nat _ _
  âŸª eq-cong e e' âŸ«  = Cat.comp-cong âŸª e âŸ« âŸª e' âŸ«
  âŸª eq-refl âŸ«       = Cat.eq-refl
  âŸª eq-sym e âŸ«      = Cat.eq-sym âŸª e âŸ«
  âŸª eq-trans e e' âŸ« = Cat.eq-trans âŸª e âŸ« âŸª e' âŸ«
