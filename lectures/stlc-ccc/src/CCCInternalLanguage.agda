{-# OPTIONS --postfix-projections #-} -- To work interactively with copatterns

module CCCInternalLanguage where

-- Setoid equality reasoning.

open import Relation.Binary using (Setoid; IsEquivalence); open Setoid; open IsEquivalence
import Relation.Binary.Reasoning.MultiSetoid as SetoidR

-- We use types as objects.

open import Types

-- Morphism of the free CCC (but lacking arbitrary morphisms)

infixl 4 _âˆ˜_

data Hom : (a b : Ty) â†’ Set where
  -- Category
  id    : âˆ€ {a} â†’ Hom a a
  _âˆ˜_   : âˆ€ {a b c} â†’ Hom b c â†’ Hom a b â†’ Hom a c  -- \ o

  -- Product
  fst   : âˆ€ {a b} â†’ Hom (a * b) a
  snd   : âˆ€ {a b} â†’ Hom (a * b) b
  pair  : âˆ€ {c a b} â†’ Hom c a â†’ Hom c b â†’ Hom c (a * b)

  -- Terminal
  unit  : âˆ€ {a} â†’ Hom a ğŸ™

  -- Exponential
  curry : âˆ€ {a b c} â†’ Hom (c * a) b â†’ Hom c (a â‡’ b)
  apply : âˆ€ {a b} â†’ Hom ((a â‡’ b) * a) b

-- Defined morphism

_âŠ—_ : âˆ€{a b c d} â†’ Hom a b â†’ Hom c d â†’ Hom (a * c) (b * d)  -- \ o x
f âŠ— g = pair (f âˆ˜ fst) (g âˆ˜ snd)

-- lift f = f âŠ— id
lift : âˆ€{c a b} â†’ Hom a b â†’ Hom (a * c) (b * c)
lift f = pair (f âˆ˜ fst) snd

uncurry : âˆ€ {a b c} â†’ Hom c (a â‡’ b) â†’ Hom (c * a) b
uncurry f = apply âˆ˜ lift f

-- Equalities

infix 0 _~_

data _~_ : âˆ€ {a b} (f g : Hom a b) â†’ Set where

  -- Category laws:

  id-l : âˆ€{a b} {f : Hom a b}
    â†’ id âˆ˜ f ~ f

  id-r : âˆ€{a b} {f : Hom a b}
    â†’ f âˆ˜ id ~ f

  assoc : âˆ€{a b c d} {f : Hom c d} {g : Hom b c} {h : Hom a b}
    â†’ (f âˆ˜ g) âˆ˜ h ~ f âˆ˜ (g âˆ˜ h)

  -- Product laws:

  -- The Î² laws.

  fst-pair : âˆ€{a b c} {f : Hom c a} {g : Hom c b}
    â†’ fst âˆ˜ pair f g ~ f

  snd-pair : âˆ€{a b c} {f : Hom c a} {g : Hom c b}
    â†’ snd âˆ˜ pair f g ~ g

  -- The Î· law.

  id-pair : âˆ€{a b}
    â†’ id {a * b} ~ pair fst snd

  -- The naturality law.

  pair-comp : âˆ€{a b c d} {h : Hom d c} {f : Hom c a} {g : Hom c b}
    â†’ pair f g âˆ˜ h ~ pair (f âˆ˜ h) (g âˆ˜ h)

  -- Law for the terminal object:  The Î· law.

  unit : âˆ€{a} {f : Hom a ğŸ™}
    â†’ f ~ unit

  -- Laws for the exponential:

  -- The Î² law.

  apply-curry : âˆ€{a b c} {f : Hom (c * a) b}
    â†’ apply âˆ˜ lift (curry f) ~ f

  -- The Î· law.

  curry-apply : âˆ€{a b}
    â†’ curry apply ~ id {a â‡’ b}

  -- The naturality law.

  curry-comp : âˆ€{a b c d} {h : Hom d c} {f : Hom (c * a) b}
    â†’ curry f âˆ˜ h ~ curry (f âˆ˜ lift h)

  -- Congruence laws:

  eq-comp : âˆ€{a b c} {f f' : Hom b c} {g g' : Hom a b}
    â†’ f ~ f'
    â†’ g ~ g'
    â†’ f âˆ˜ g ~ f' âˆ˜ g'

  eq-pair : âˆ€{a b c} {f f' : Hom a b} {g g' : Hom a c}
    â†’ f ~ f'
    â†’ g ~ g'
    â†’ pair f g ~ pair f' g'

  eq-curry : âˆ€{a b c} {f f' : Hom (c * a) b}
    â†’ f ~ f'
    â†’ curry f ~ curry f'

  -- Equivalence laws:

  eq-refl : âˆ€{a b} {f : Hom a b}
    â†’ f ~ f

  eq-sym : âˆ€{a b} {f g : Hom a b}
    â†’ g ~ f
    â†’ f ~ g

  eq-trans : âˆ€{a b} {f g h : Hom a b}
    â†’ f ~ g
    â†’ g ~ h
    â†’ f ~ h

-- Each Homset is a setoid.

homSetoid : (a b : Ty) â†’ Setoid _ _
homSetoid a b .Carrier = Hom a b
homSetoid a b ._â‰ˆ_     = _~_
homSetoid a b .isEquivalence .refl  = eq-refl
homSetoid a b .isEquivalence .sym   = eq-sym
homSetoid a b .isEquivalence .trans = eq-trans

-- Derived laws.

open SetoidR

-- A more general Î·-law.

curry-apply' : âˆ€{a b c} (f : Hom c (a â‡’ b))
  â†’ curry (apply âˆ˜ lift f) ~ f

curry-apply' f = beginâŸ¨ homSetoid _ _ âŸ©
  curry (apply âˆ˜ lift f)   â‰ˆâŸ¨ eq-sym curry-comp âŸ©
  curry apply âˆ˜ f          â‰ˆâŸ¨ eq-comp curry-apply eq-refl âŸ©
  id âˆ˜ f                   â‰ˆâŸ¨ id-l âŸ©
  f
  âˆ

-- Contraction of lifting and pairing.

lift-pair : âˆ€ {a b c} (f : Hom c b) (g : Hom c a)
  â†’ lift f âˆ˜ pair id g ~ pair f g
lift-pair f g = beginâŸ¨ homSetoid _ _ âŸ©
    lift f âˆ˜ pair id g
  â‰ˆâŸ¨ pair-comp âŸ©
    pair (f âˆ˜ fst âˆ˜ pair id g) (snd âˆ˜ pair id g)
  â‰ˆâŸ¨ eq-pair
       (beginâŸ¨ homSetoid _ _ âŸ©
         f âˆ˜ fst âˆ˜ pair id g     â‰ˆâŸ¨ assoc âŸ©
         f âˆ˜ (fst âˆ˜ pair id g)   â‰ˆâŸ¨ eq-comp eq-refl fst-pair âŸ©
         f âˆ˜ id                  â‰ˆâŸ¨ id-r âŸ©
         f                       âˆ)
       snd-pair âŸ©
    pair f g
  âˆ

-- A more familiar Î²-law.

beta : âˆ€ {a b c} (f : Hom (c * a) b) (g : Hom c a)
  â†’ apply âˆ˜ pair (curry f) g ~ f âˆ˜ pair id g
beta f g = beginâŸ¨ homSetoid _ _ âŸ©
    apply âˆ˜ pair (curry f) g
  â‰ˆâŸ¨ eq-comp eq-refl (eq-sym (lift-pair (curry f) g)) âŸ©
    apply âˆ˜ (lift (curry f) âˆ˜ pair id g)
  â‰ˆâŸ¨ eq-sym assoc âŸ©
    apply âˆ˜ lift (curry f) âˆ˜ pair id g
  â‰ˆâŸ¨ eq-comp apply-curry eq-refl âŸ©
    f âˆ˜ pair id g
  âˆ

-- Uniqueness properties for the various mediating morphisms.

pair-unique : âˆ€ {a b c} (f : Hom c a) (g : Hom c b) (h : Hom c (a * b)) â†’
              fst âˆ˜ h ~ f â†’ snd âˆ˜ h ~ g â†’ h ~ pair f g
pair-unique f g h hypâ‚ hypâ‚‚ = beginâŸ¨ homSetoid _ _ âŸ©
  h                          â‰ˆâŸ¨ eq-sym (id-l) âŸ©
  id âˆ˜ h                     â‰ˆâŸ¨ eq-comp id-pair eq-refl âŸ©
  pair fst snd âˆ˜ h           â‰ˆâŸ¨ pair-comp âŸ©
  pair (fst âˆ˜ h) (snd âˆ˜ h)   â‰ˆâŸ¨ eq-pair hypâ‚ hypâ‚‚ âŸ©
  pair f g                   âˆ

curry-unique : âˆ€ {a b c} (f : Hom (c * a) b) (h : Hom c (a â‡’ b)) â†’
               apply âˆ˜ lift h ~ f â†’ h ~ curry f
curry-unique f h hyp = beginâŸ¨ homSetoid _ _ âŸ©
  h                      â‰ˆâŸ¨ eq-sym id-l âŸ©
  id âˆ˜ h                 â‰ˆâŸ¨ eq-comp (eq-sym curry-apply) eq-refl âŸ©
  curry apply âˆ˜ h        â‰ˆâŸ¨ curry-comp âŸ©
  curry (apply âˆ˜ lift h) â‰ˆâŸ¨ eq-curry hyp âŸ©
  curry f                âˆ
