{-
This is the companion code for a talk at the Initial Types Club, Chalmers
University GÃ¶teborg, Feb 7 2019, by Jannis Limperg
(email: <my first name>@<my last name>.de).

Title: Proof by Reflection

Abstract:

Proof by reflection (which is only superficially related to Agda's Reflection
metaprogramming facilities) is a proof technique for dependently typed
languages. It is typically used to build solvers for a class of problems, for
example determining whether a formula of propositional logic is true. These
solvers can replace lengthy manual proofs, and they are typically more efficient
than tactics based on metaprogramming.

I will live-code the second-most boring example of proof by reflection: a
simplifier for equations in a monoid. Afterwards, I'll talk briefly about
variations on the basic technique and more interesting applications.
-}

module proof-by-reflection where

open import Relation.Binary.PropositionalEquality using
  (_â‰¡_ ; refl ; sym ; trans ; cong ; cong-app ; module â‰¡-Reasoning)

open â‰¡-Reasoning


-- # Basic Template

{-
This section shows the implementation of a reflective solver that generates
proofs for equalities in arbitrary monoids. It does so by, essentially,
normalising both sides of the equation and checking whether the normal forms are
equal. The implementation proceeds in four steps.
-}

module Basic where

  record Monoid : Setâ‚ where
    field
      Carrier : Set
      zero    : Carrier
      plus    : Carrier â†’ Carrier â†’ Carrier

      zero-l : âˆ€ {x}     â†’ plus zero x â‰¡ x
      zero-r : âˆ€ {x}     â†’ plus x zero â‰¡ x
      assoc  : âˆ€ {x y z} â†’ plus (plus x y) z â‰¡ plus x (plus y z)

  open Monoid public


  {-
  Step 1: Define a syntactic representation, `Tm`, for expressions in a monoid:

  - `ğŸ˜` represents the monoid's zero/unit element.
  - `_+_` represents the monoid's binary operation.
  - `[ x ]` is called an uninterpreted term. Our simplifier will treat it as an
    arbitrary element of the carrier set.
  -}

  data Tm (Carrier : Set) : Set where
    [_]  : Carrier            â†’ Tm Carrier
    ğŸ˜    :                      Tm Carrier
    _+_  : (t u : Tm Carrier) â†’ Tm Carrier


  {-
  Step 2: Define the denotation (meaning) of a term in a given monoid.
  -}

  tmD : (M : Monoid) â†’ Tm (Carrier M) â†’ Carrier M
  tmD M [ x ]   = x
  tmD M ğŸ˜       = zero M
  tmD M (t + u) = plus M (tmD M t) (tmD M u)


  {-
  Step 3: Define a function which simplifies a term. The simplification must
  not change the meaning of a term ('soundness'). In also shouldn't miss any
  simplification opportunities ('completeness').
  -}

  simp : âˆ€ {Carrier} â†’ Tm Carrier â†’ Tm Carrier
  simp [ x ] = [ x ]
  simp ğŸ˜     = ğŸ˜
  simp (t + u) with simp t | simp u
  ... | [ x ]    | [ y ]    = [ x ] + [ y ]
  ... | [ x ]    | ğŸ˜        = [ x ]
  ... | [ x ]    | su + suâ€² = [ x ] + (su + suâ€²)
  ... | ğŸ˜        | su       = su
  ... | st + stâ€² | [ x ]    = st + (stâ€² + [ x ])
  ... | st + stâ€² | ğŸ˜        = st + stâ€²
  ... | st + stâ€² | su + suâ€² = st + (stâ€² + (su + suâ€²))

  -- The `simp` given during the talk was incomplete, as it missed a
  -- simplification opportunity. I hope that the above is now complete (but no
  -- proof!).


  {-
  Step 4: Prove that `simp` is, indeed, sound in the above sense.
  -}

  simp-correct : (M : Monoid) (t : Tm (Carrier M))
    â†’ tmD M t â‰¡ tmD M (simp t)
  simp-correct M [ x ]    = refl
  simp-correct M ğŸ˜        = refl
  simp-correct M (t + u)
    rewrite simp-correct M t | simp-correct M u
    with simp t    | simp u
  ... | [ x ]      | [ y ]    = refl
  ... | [ x ]      | ğŸ˜        = zero-r M
  ... | [ x ]      | su + suâ€² = refl
  ... | ğŸ˜          | su       = zero-l M
  ... | st + stâ€²   | [ x ]    = assoc M
  ... | st + stâ€²   | ğŸ˜        = zero-r M
  ... | st + stâ€²   | su + suâ€² = assoc M


  {-
  That's it! Now we can prove annoying lemmas like the following by applying
  `simp-correct` to a syntactic representation of the equation's left-hand side
  (provided that the right-hand is the normal form of the right-hand side).
  -}

  annoying : (M : Monoid) (x y z : Carrier M)
    â†’ let open Monoid M renaming (plus to _âŠ•_ ; zero to O) in

      ((O âŠ• x) âŠ• y) âŠ• (z âŠ• O) â‰¡ x âŠ• (y âŠ• z)

  annoying M x y z = simp-correct M (((ğŸ˜ + [ x ]) + [ y ]) + ([ z ] + ğŸ˜))


-- # Extensions

-- ## Symmetric Simplification

{-
`simp-correct` only works if the right-hand side of the equation is the normal
form of the left-hand side. We usually want a variation on the technique where
we only require that the left-hand side and the right-hand side have the same
normal form.
-}

module Sym where

  open Basic

  simp-correct-sym : (M : Monoid) (t u : Tm (Carrier M))
    â†’ simp t â‰¡ simp u
    â†’ tmD M t â‰¡ tmD M u
  simp-correct-sym M t u eq =
    begin
      tmD M t
    â‰¡âŸ¨ simp-correct M t âŸ©
      tmD M (simp t)
    â‰¡âŸ¨ cong (tmD M) eq âŸ©
      tmD M (simp u)
    â‰¡âŸ¨ sym (simp-correct M u) âŸ©
      tmD M u
    âˆ


  more-annoying : (M : Monoid) (x y z : Carrier M)
    â†’ let open Monoid M renaming (plus to _âŠ•_ ; zero to O) in

      ((O âŠ• x) âŠ• y) âŠ• (z âŠ• O) â‰¡ (x âŠ• y) âŠ• (z âŠ• O)

  more-annoying M x y z = simp-correct-sym M
    (((ğŸ˜ + [ x ]) + [ y ]) + ([ z ] + ğŸ˜))
    (([ x ] + [ y ]) + ([ z ] + ğŸ˜))
    refl


-- ## Constructing Tm by Reflection

{-
So far, we have had to write syntactic representations of our terms by hand.
Given that there is a very obvious correspondence between the terms and their
syntactic counterparts, we can enlist the help of Agda's metaprogramming
facilities (confusingly also called 'reflection') to write the syntactic
representations for us. This is conceptually straightforward, but the
implementation is a bit annoying, so I only give an outline.
-}

module Reflection where

  open import Data.Unit using (âŠ¤)
  open import Reflection using (Term ; TC)

  open Basic using (Monoid)

  macro
    simp! : Monoid â†’ Term â†’ TC âŠ¤
    simp! M hole = {!!}

  {-
  Implementation outline:

  - Infer type of `hole`.
  - Match type of `hole` with `?t â‰¡ ?u`, where `t, u : Carrier M`
  - Construct `T, U` such that `tmD M T â‰¡ t` and `tmD M U â‰¡ u`.
  - Fill `hole` with `simp-correct-sym M T U refl`.

  Usage:

  `more-annoying M x y z = simp! M`
  -}


-- ## Environment for Uninterpreted Terms

{-
So far, we have included uninterpreted terms in the `Tm` datatype directly. In
practice, we usually want to store them in an environment (here simply a vector)
which `Tm` indexes into. The advantage of doing so is that `Tm` then has
decidable propositional equality and a total ordering.

This is important for normalisation procedures in richer algebraic structures.
For example, in a commutative monoid, we need to decide whether the normal form
of `x + y` is `x + y` or `y + x`. One way to do so is to look at whether the
index of the uninterpreted term `x` is smaller than that of `y`.
-}

module Env where

  open import Data.Fin using (Fin)
  open import Data.Nat using (â„•)
  open import Data.Vec using (Vec ; [] ; _âˆ·_ ; lookup)

  open Basic using (Monoid ; Carrier ; zero ; plus)


  Env : Set â†’ â„• â†’ Set
  Env = Vec


  data Tm (n : â„•) : Set where
    [_] : Fin n â†’ Tm n
    ğŸ˜ : Tm n
    _+_ : (x y : Tm n) â†’ Tm n


  tmD : (M : Monoid) â†’ âˆ€ {n} â†’ Env (Carrier M) n â†’ Tm n â†’ Carrier M
  tmD M Î“ [ x ] = lookup Î“ x
  tmD M Î“ ğŸ˜ = zero M
  tmD M Î“ (t + u) = plus M (tmD M Î“ t) (tmD M Î“ u)


  -- etc.


-- ## Multi-Sorted Term Language

{-
So far, our term languages have been unityped: Any term represents an element of
some monoid's carrier set. If we want to model richer problem domains, we need
to work with typed term languages instead. For example, if our language contains
descriptions of monoid homomorphisms in addition to descriptions of monoid
elements, then the term

    F x + G y

only makes sense (i.e., has a denotation) if `x` is an element of monoid `M`, `y`
is an element of monoid `N`, `F` is a homomorphism from `M` to `O` and `G` is
a homomorphism from `N` to `O`.

I don't yet know the best way to deal with these more complicated domains. I can
only report that they come with a bag of problems:

- The term language must be either intrinsically or extrinsically typed.
  Intrinsic typing is problematic for complex type systems. Extrinsic typing is
  better understood, but requires that we either write or generate a typing
  derivation when calling the reflective tactic.
- Similarly, the environments now need to be typed. They can be stratified into
  different environments for different types/sorts or implemented as telescopes.
- If the involved objects (e.g. monoids) are at different universe levels, they
  must be lifted to the maximum of all universe levels. This can be done
  manually or via metaprogramming.
-}


-- ## Composable Reflective Tactics

{-
See Gregory Malecha's papers/dissertation on 'Rtac'.

High-level overview: https://gmalecha.github.io/reflections/2016/rtac-technical-overview
-}


-- # Bonus: Monoid Normalisation Via the Free Monoid

{-
... as suggested by Andreas Abel during the talk. The free monoid over a
carrier set `C` is simply `List C`.
-}

module Free where

  open import Algebra.FunctionProperties using (Associative)
  open import Data.List using (List ; [] ; _âˆ·_ ; _++_ ; foldr)
  open import Data.List.Properties using (foldr-++)

  open Basic using (Monoid ; module Monoid ; Tm ; ğŸ˜ ; _+_ ; [_] ; tmD)


  nf : âˆ€ {Carrier} â†’ Tm Carrier â†’ List Carrier
  nf [ x ] = x âˆ· []
  nf ğŸ˜ = []
  nf (t + u) = nf t ++ nf u


  module _ (M : Monoid) where

    open Monoid M renaming (zero to O ; plus to _âŠ•_)


    nfD : List Carrier â†’ Carrier
    nfD = foldr _âŠ•_ O


    nfD-++ : âˆ€ xs ys â†’ nfD xs âŠ• nfD ys â‰¡ nfD (xs ++ ys)
    nfD-++ []       ys = zero-l
    nfD-++ (x âˆ· xs) ys = trans assoc (cong (x âŠ•_) (nfD-++ xs ys))


    nf-correct : (t : Tm Carrier) â†’ tmD M t â‰¡ nfD (nf t)
    nf-correct [ x ]   = sym zero-r
    nf-correct ğŸ˜       = refl
    nf-correct (t + u)
      rewrite nf-correct t | nf-correct u
      = nfD-++ (nf t) (nf u)


    nf-correct-sym : (t u : Tm Carrier)
      â†’ nf t â‰¡ nf u
      â†’ tmD M t â‰¡ tmD M u
    nf-correct-sym t u eq
      = begin
          tmD M t
        â‰¡âŸ¨ nf-correct t âŸ©
          nfD (nf t)
        â‰¡âŸ¨ cong nfD eq âŸ©
          nfD (nf u)
        â‰¡âŸ¨ sym (nf-correct u) âŸ©
          tmD M u
        âˆ


    more-annoying : âˆ€ x y z â†’
      ((O âŠ• x) âŠ• y) âŠ• (z âŠ• O) â‰¡ (x âŠ• y) âŠ• (z âŠ• O)
    more-annoying x y z = nf-correct-sym
      (((ğŸ˜ + [ x ]) + [ y ]) + ([ z ] + ğŸ˜))
      (([ x ] + [ y ]) + ([ z ] + ğŸ˜))
      refl
